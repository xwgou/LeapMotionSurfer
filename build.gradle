group = 'de.mfo.jsurf'
version = '0.1.0'

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

targetCompatibility = 1.6
sourceCompatibility = 1.6

repositories {
    mavenCentral()
    maven {
        url "http://download.tuxfamily.org/arakhne/maven"
    }
}

dependencies {
    compile group: 'javax', name: 'vecmath', version: '1.5.2'
    compile 'org.antlr:antlr-runtime:3.4@jar'
    compile 'commons-cli:commons-cli:1.2'
    compile files('lib/leapMotion/leapJava.jar')
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.3'
}

uploadArchives {
    repositories.mavenDeployer {
	// TODO: change to official maven repo
	repository( url: "file:///media/DATA/IMAGINARY/maven-repository" )

	// workflow for deploying: 
	// clone/pull https://github.com/IMAGINARY/maven-repository.git into '/media/DATA/IMAGINARY/maven-repository' 
	// gradle uploadArchives
	// in '/media/DATA/IMAGINARY/maven-repository': git push origin master
    }
}

mainClassName = "de.mfo.jsurf.Main"

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

distZip {
	folderName = archiveName.replace("." + extension, "")
    into(folderName) {
    	from '.'
        include 'lib/leapMotion/*'
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Djava.library.path=../lib/leapMotion"')
    }
}

run {
	//jvmArgs needed to run leapMotion library
	jvmArgs '-Djava.library.path=lib/leapMotion'
   if ( project.hasProperty('args') ) {
       args project.args.split('\\s+')
   }
}



